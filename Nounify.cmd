#REQUIRE Error.cmd
gosub Nounify %0
exit

Nounify:
	var Nounify.fullItemName $0
	if "%Nounify.fullItemName" == "" then {
		gosub Error Nounify called with no string.
		return
	}
	var Nounify.noun %Nounify.fullItemName
	put #tvar Nounify.noun null
	# Fix for deeds: (technically, this is two words, but more helpful. Might need to do this with other items as well.)
	if (matchre("%Nounify.noun", "a deed for (a|an|some) (heavy|fine|huge|large|massive|medium|small|thick|thin|tiny) (\S+) \w+")) then {
		# First case, deed has an additional size adjective.
		var Nounify.noun $3 deed
		goto NounifyReturn
	}
	if (matchre("%Nounify.noun", "a deed for (a|an|some) (\S+) \w+")) then {
		var Nounify.noun $2 deed
		goto NounifyReturn
	}
	# Crafting instruction books (a book of master blacksmithing instructions):
	if (matchre("%Nounify.noun", "a book of .+ instructions")) then {
		var Nounify.noun book
		goto NounifyReturn
	}
	# Random/Odd suffixes first:
	eval Nounify.noun replacere("%Nounify.noun", " (bearing a|contained within|crafted to resemble|designed as a tribute|edged in|emblazoned with|embossed with|etched with|exquisitely embroidered with|hanging from|labeled with|resting in|set in|set with|suspended from|worked in a web-like) .+", "")
	# With word preceeding 'with' suffix:
	eval Nounify.noun replacere("%Nounify.noun", " (adorned|decorated|embroidered|painted|sealed|trimmed) with .+", "")
	# Just the 'with' suffix:
	eval Nounify.noun replacere("%Nounify.noun", " (with|of) .+", "")
	# Special fix for a list generated by Inventory which contains a closed container:
	eval Nounify.noun replacere("%Nounify.noun", " \(?closed\)?$", "")
	# Suffix removed, now last word should be the noun:
	eval Nounify.noun replacere("%Nounify.noun", ".+ ", "")
NounifyReturn:
	#echo Noun is %Nounify.noun
	return